/*
Kenneth Corrales Lizano carnet 2017151342 
Alejandro Tapia Alvarez carnet 2017116952

Tabla de valores realizado con 100 000 iteraciones
Q/R		10		20		30		40		50		60		70		80		90		100
100	310060.6388	363389.1024	430892.6676	502564.4707	574871.9045	647508.5494	720439.668	793651.1806	867085.055	940195.7546

90	275800.9239	327292.1223	394382.3654	466256.3749	538713.2919	611414.8998	684529.6744	757901.0754	830815.664	

80	242402.2719	291401.4801	358066.6984	430000.2031	502603.1439	575484.5738	648658.1626	721598.7098		

70	210009.1678	255873.7499	321908.2538	393878.6248	466654.1792	539730.1496	612499.5144			

60	179236.8735	220819.9421	285983.5126	357983.5031	430908.9631	503705.6392				

50	150940.1405	186512.8771	250358.5116	322389.8982	395129.8408					

40	126989.5505	153472.9277	215288.7996	286995.7393						

30	111735.6406	122850.5259	180925.6833							

20	129120.8924	99835.2275								

10	6749333.088									


Por lo tanto, los mejores valores de q y r respectivamente son: q=20 r=20






*/

clearoutput




/*
Esta Funcion se utiliza para saber la cantidad de dias que puede durar 
en llegar la mercaderia
*/
newcmd calcularDias diasRestantes
   uniform 1 0 1 temp
   if temp < 0.30
      diasRestantes = 1
   elseif temp <0.70
      diasRestantes = 2
   else
      diasRestantes = 3
   end
end

/*
Esta funcion se encarga de ordenar el inventario, de acuerdo a los diversos casos,
en sintesis revisa el inventario actual, calcula la venta del dia, reduce el inventario y dependiendo de si se
solicito o no nueva mercaderia la coloca en el inventario disponible del dia siguiente
*/ 
newcmd actualizarInventario Ini Final inventario  Venta bool dia q costoInventario costoPenalizacion
   
   poisson 1 7 ventaDia	'Calcula la venta del dia
   put ventaDia dia Venta
   inventario = inventario - ventaDia ' Reduce del inventario lo que se vende durante el dia
   if inventario > 0	'Para calcular el costo final del inventario
      costoInventario = costoInventario + inventario
   else
      tmp = -1 * inventario	'Para calcular la penalizacion
      costoPenalizacion = costoPenalizacion + tmp
   end
   put inventario dia Final ' Guarda el inventario como quedo al final del dia
   temp2 = dia + 1
   if bool = 1 'Significa que se debe de sumar al inventario lo que llega de mercaderia nueva
      if temp2 < 366
         inventario = inventario + q
         put inventario temp2 Ini	'Guarda el inventario para que quede listo para el dia siguiente
      end
   else 'Solo se alista la mercaderia que hay actualmente
      if temp2 < 366
         put inventario temp2 Ini
      end
   end
end






newcmd calcularInventario q r Ini PedirQ Dias Venta Final N costoFijo costoInventario costoPenalizacion costoTotal
   inventario = q 'Se inicia anadiendo al inventario inicial q cantidad de mercaderia
   diasRestantes = 0
   copy Ini q Ini 'Se inicia anadiendo al inventario inicial q cantidad de mercaderia
   foreach dia N	'Por cada dia del anio
      if inventario < r ' Si queda poco inventario
         put 1 dia PedirQ 'Se pide inventario
         if diasRestantes = 0 'Si queda poco inventario y todavia no se ha pedido
            costoFijo = costoFijo + 100
            calcularDias diasRestantes 'Se calculan los dia para que llegue el camion
            put diasRestantes dia dias	
            if diasRestantes = 1 'Significa que para el dia siguiente se empieza con la nueva mercaderia en el inventario
               actualizarInventario Ini Final inventario  Venta 1 dia q costoInventario costoPenalizacion
               diasRestantes = 0
            else
               actualizarInventario Ini Final inventario  Venta 0 dia q costoInventario costoPenalizacion
            end
            
         else
            diasRestantes = diasRestantes - 1
            put diasRestantes dia dias
            if diasRestantes = 1
               actualizarInventario Ini Final inventario  Venta 1 dia q costoInventario costoPenalizacion
               diasRestantes = 0
               
            else
               actualizarInventario Ini Final inventario  Venta 0 dia q costoInventario costoPenalizacion
            end
         end
      else
         'Un dia comun donde el inventario es suficiente y el inventario es superior  a r
         put 0 dia PedirQ
         put 0 dia Dias
         actualizarInventario Ini Final inventario  Venta 0 dia q costoInventario costoPenalizacion
      end
   end
	'Calcula el costo final 
   costoInventario = costoInventario * 20
   costoPenalizacion = costoPenalizacion * 50
   costoTotal = costoInventario + costoPenalizacion + costoFijo
end


/*
Programa Principal
*/

costoFijo = 0
costoInventario = 0
costoPenalizacion = 0
costoTotal = 0


Ini = ()
PedirQ = ()
Dias = ()
Venta = ()
Final = ()
copy 1,365 N

q = 50
r = 20
calcularInventario q r Ini PedirQ Dias Venta Final N costoFijo costoInventario costoPenalizacion costoTotal

print table N%8.0F Ini%8.0F PedirQ%8.0F Dias%8.0F Venta%8.0F Final%8.0F

print costoFijo
print costoInventario
print costoPenalizacion
print costoTotal






