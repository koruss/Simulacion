/*
Kenneth Corrales Lizano carnet 2017151342
Alejandro Tapia Alvarez carnet 2017116952
La probabilidad del jugador de ganar es: 42,84%

Codigo del Programa
*/
clearoutput 

'Cuenta la el valor de las cartas en una mano
newcmd contarCartas mano resultado
	resultado = 0 
	recode mano > 10 10 manoTemp
	sum manoTemp resultado
	count manoTemp = 1 contAs
	if contAs <> 0 'Si hay ases
		faltante = 21-resultado
		divide faltante 10 faltante
		integer floor faltante faltante
		' Mientras hayan ases que valgan 1 y mientras falte más de 10 para llegar a 21 le agrega 10
		while contAs > 0 and faltante > 0 
			resultado = resultado +10
			contAs = contAs - 1
			faltante= faltante - 1
		end  
	end
end

'Utiliza la tabla dada por el profesor para calcular si el jugador debe pedir carta o quedarse
newcmd calcularJugada suma cartaVisible decision
	recode cartaVisible = 1 11 cartaVisible
	if suma <=11 
		decision = 1 'Hit
	elseif suma =12 and cartaVisible notbetween  4 6
		decision = 1
	elseif suma between 13 16 and cartaVisible between 7 13
		decision = 1
	else
		decision = 0 'Stay
	end
end

'Simula el juego de 21 con una baraja nueva cada vez
newcmd jugar isGanador
	copy 4#1,13 deck 
	shuffle deck deck 
	take deck ( 1 3) misCartas
	take deck (2 4) casaCartas
	remove deck 1,4 deck
	resultado = 0
	resultadoCasa = 0
	decision = 1
	contarCartas misCartas resultado 'cuenta el resultado de la mano
	take casaCartas 2 cartaVisible 'veo la carta de la casa
	'genera un loop para seguir jugando si es necesario
	calcularJugada resultado cartaVisible decision
	while resultado<21 and decision = 1
		take deck 1 nuevaCarta
		remove deck 1 deck
		copy misCartas nuevaCarta misCartas
		contarCartas misCartas resultado
		calcularJugada resultado cartaVisible decision
	end
	contarCartas casaCartas resultadoCasa

	'genera un loop para simular las jugadas de la casa
	while resultadoCasa<17
		take deck 1 nuevaCarta
		remove deck 1 deck
		copy casaCartas nuevaCarta casaCartas
		contarCartas casaCartas resultadoCasa
	end
	/*Si es menor a 22 y, o es mayor que la casa o el resultado de la casa es mayor a 21 gana
	sino, pierde*/
	if resultado < 22 and ( resultado > resultadoCasa or resultadoCasa > 21)
		isGanador = 1
	else
		isGanador = 0
	end
/*
	print misCartas
	print casaCartas
	print resultado 
	print resultadoCasa
	print isGanador		
*/
	
end
isGanador = 0
total = 0 
repeticiones = 1000000
repeat repeticiones
	jugar isGanador
	total = total + isGanador
end

probabilidad = total/repeticiones
	
print total
print probabilidad



		


